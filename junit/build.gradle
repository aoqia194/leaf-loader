apply plugin: 'maven-publish'

base {
	archivesName = "${rootProject.name}-junit"
}

version = rootProject.version
group = rootProject.group

repositories {
	mavenCentral()
}

dependencies {
	api project(':')

	api platform('org.junit:junit-bom:5.10.0')
	api 'org.junit.jupiter:junit-jupiter-engine'
	implementation 'org.junit.platform:junit-platform-launcher'
}

java {
	withSourcesJar()
    withJavadocJar()
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
	manifest {
		attributes 'Automatic-Module-Name': 'dev.aoqia.leaf.loader.junit'
	}
}

publishing {
	publications {
		maven(MavenPublication) {
            groupId project.group
            artifactId project.base.archivesName.get()
            version project.version

            pom {
                name = rootProject.name
                group = rootProject.group
                description = rootProject.description
                url = rootProject.url
                inceptionYear = '2025'
                developers {
                    developer {
                        id = 'aoqia'
                        name = 'aoqia'
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = "${rootProject.url}/issues"
                }
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/aoqia194/leaf-" + rootProject.name + "/.git"
                    developerConnection = "scm:git:ssh://github.com/aoqia194/leaf-" + rootProject.name + "/.git"
                    url = rootProject.url
                }
            }

            artifact jar
            artifact sourcesJar
            artifact javadocJar
		}
	}

    repositories {
        maven {
            url = rootProject.layout.buildDirectory.dir('staging-deploy')
        }
    }
}
