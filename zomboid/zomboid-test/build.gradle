apply plugin: 'dev.aoqia.leaf.loom'

loom {
    clientOnlyZomboidJar()

    runConfigs.configureEach {
        ideConfigGenerated = true
        property('leaf.debug.replaceVersion', "leafloader:${version}")
    }
}

def zomboid_version = '42.8.1-unstable.29171'
def zomboid_mappings = '41.78.16+build.1'

repositories {
    mavenCentral()
}

dependencies {
    zomboid "com.theindiestone:zomboid:${zomboid_version}"
    mappings "dev.aoqia.leaf:yarn:${zomboid_mappings}:v2"

    implementation project(':zomboid')
    implementation project(':zomboid').sourceSets.main.output
    implementation project(':').sourceSets.main.output
    implementation(project(path: ':mixinextras-leaf', configuration: 'shadow'))

    testImplementation project(':junit')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(':mixinextras-leaf:shadowJar')

    it.options.encoding = "UTF-8"
    it.options.release = 17
}


import dev.aoqia.leaf.loom.util.Platform
import groovy.json.JsonSlurper

configurations {
    productionRuntime {
        extendsFrom configurations.zomboidLibraries
        extendsFrom configurations.loaderLibraries
        extendsFrom configurations.zomboidRuntimeLibraries
    }
    productionRuntimeMods {
        transitive = false
    }
}

dependencies {
    // Include the external libraries on the classpath
    def installerJson = new JsonSlurper().parse(rootProject.file("src/main/resources/leaf-installer.json"))
    installerJson.libraries.common.each {
        productionRuntime it.name
    }

    // Use Fabric's auto client test
    //	productionRuntimeMods "dev.aoqia.fabric-api:fabric-api:0.89.3+1.20.2"
    //	productionRuntimeMods "dev.aoqia.fabric-api:fabric-api:0.89.3+1.20.2:testmod"
}

def loaderJarTask = project(":").tasks.finalJar

// This is very far beyond loom's API if you copy this, you're on your own.
//task runProductionAutoTestClient(type: JavaExec, dependsOn: [loaderJarTask, remapJar]) {
task runProductionAutoTestClient(type: JavaExec, dependsOn: [loaderJarTask]) {
    classpath.from configurations.productionRuntime
    classpath.from loaderJarTask
    mainClass = "dev.aoqia.leaf.loader.impl.launch.knot.KnotClient"

    // Let loom set it instead.
//    workingDir = file("run")

    afterEvaluate {
        //		dependsOn downloadAssets
    }

    doFirst {
        classpath.from loom.zomboidProvider.zomboidClientJar
        workingDir.mkdirs()

        if (Platform.CURRENT.operatingSystem.isMacOS()) {
            jvmArgs(
                "-XstartOnFirstThread"
            )
        }

        def modFiles = []
        modFiles.addAll configurations.productionRuntimeMods.files
        modFiles.add remapJar.archiveFile.get().asFile
        def mods = modFiles.join(File.pathSeparator)

        jvmArgs(
            "-Dleaf.addMods=${mods}",
            "-Dleaf.autoTest"
        )
    }
}
